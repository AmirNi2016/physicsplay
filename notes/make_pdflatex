#!/usr/bin/perl

use strict ;
use warnings ;
use Getopt::Long;

my $rc = $? ;
my $built = 0 ;
my $forcebib = 0 ;
my $pdflatex = 'pdflatex' ;
my $verbose = 1 ;
my $filename ;
my $justonce = 0 ;

GetOptions(
   'filename=s'   => \$filename,
   'forcebib!'    => \$forcebib,
   'justonce!'    => \$justonce,
   'verbose!'     => \$verbose
) ;

die "usage: make_pdflatex [-forcebib] [-[no]verbose] -filename filename.tex\n" if (! defined $filename) ;

#my $pwd = `pwd` ; chomp $pwd ;

# http://tex.stackexchange.com/questions/7204/errors-warnings-in-latex-output
$pdflatex .= ' -file-line-error' ;

for ( my $i = 0 ; $i < 3 ; $i++ )
{
   my $genbib = 0 ;
   my $genindex = 0 ;
   my $indexFile ;

   my $basename = $filename ;
   $basename =~ s/\..*// ;

   print "$i: # $pdflatex $filename\n" if ( $verbose ) ;

   open my $fh, "$pdflatex $filename| " or die "could not run '$pdflatex' or open file '$filename'\n" ;

   while (<$fh>)
   {
      print ;
      if (    /Rerun to get cross-references/
           or /There were multiply-defined labels/
           or /Warning: Citation/
           or /Warning: Label\(s\) may have changed./
           or /Rerun to get outlines right/
           or ($forcebib and !$i)
         )
      {
         $genbib = 1 ;
      }

      # only do the makeindex on the first pass.
      if ( /Writing index file (\S+)/ and !$i )
      {
         $indexFile = $1 ;
         $genindex = 1 ;
      }
   }
   close $fh ;
   $rc = $? ;

   if ( $genindex )
   {
      print "$i: # makeindex $indexFile\n" if ( $verbose ) ;
      system( "makeindex $indexFile" ) ;
   }  

   if ( $genbib )
   {
      print "$i: # bibtex $basename\n" if ( $verbose ) ;
      system( "bibtex $basename" ) ;
   }

   last unless ( $genbib or $genindex ) ;

   exit if ($justonce) ;
}

exit $rc ;
