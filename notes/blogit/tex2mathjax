#! /usr/bin/perl

# TODO for wp-mathjax:
#
# - find the inline equations and change them to: \( \)
#  ... should just use \( \) directly in new writing.
#
# - title and prologue
#
# - \cref{FigN} -> fig. N

use strict ;
use warnings ;
use Getopt::Long;

my $filebase ;
my $showUsage = 0 ;

my $urlMessage ;
$urlMessage = "Click here for a PDF of this post with nicer formatting" ;

my $r1 = qr/
     (                   # start of capture buffer 1
     {                   # match an opening brace
         (?:               
             [^{}]++     # one or more non braces, non backtracking
               |                  
             (?1)        # found { or }, so recurse to capture buffer 1
         )*                 
     }                   # match a closing brace
     )                   # end of capture buffer 1
     /x;

my $r2 = qr/
     (                   # start of capture buffer 1
     {                   # match an opening brace
         (?:               
             [^{}]++     # one or more non braces, non backtracking
               |                  
             (?1)        # found { or }, so recurse to capture buffer 1
         )*                 
     }                   # match a closing brace
     )                   # end of capture buffer 1
     (                   # start of capture buffer 2
     {                   # match an opening brace
         (?:               
             [^{}]++     # one or more non braces, non backtracking
               |                  
             (?1)        # found { or }, so recurse to capture buffer 2
         )*                 
     }                   # match a closing brace
     )                   # end of capture buffer 2
     /x;

my $r3 = qr/
     (                   # start of capture buffer 1
     {                   # match an opening brace
         (?:               
             [^{}]++     # one or more non braces, non backtracking
               |                  
             (?1)        # found { or }, so recurse to capture buffer 1
         )*                 
     }                   # match a closing brace
     )                   # end of capture buffer 1
     (                   # start of capture buffer 2
     {                   # match an opening brace
         (?:               
             [^{}]++     # one or more non braces, non backtracking
               |                  
             (?1)        # found { or }, so recurse to capture buffer 2
         )*                 
     }                   # match a closing brace
     )                   # end of capture buffer 2
     (                   # start of capture buffer 3
     {                   # match an opening brace
         (?:               
             [^{}]++     # one or more non braces, non backtracking
               |                  
             (?1)        # found { or }, so recurse to capture buffer 3
         )*                 
     }                   # match a closing brace
     )                   # end of capture buffer 3
     /x;


GetOptions( 
   'file=s'       => \$filebase,
   'help!'        => \$showUsage,
) ;

if ( $showUsage or !defined $filebase )
{
   die "usage: ~/bin/tex2mathjax -f filenameWithoutTexSuffix [-help]" ;
}

my $rLines = slurpLatex( $filebase ) ;

print "[mathjax]\n" ;

foreach (@$rLines)
{
   chomp ;

   s,\\renewcommand{\\basename}{(.*?)},\\blogpage{http://peeterjoot.com/archives/math2014/$1.pdf}, ;
   s,\\blogpage{(.*)},<a href="$1">[$urlMessage]</a>, ;

   # FIXME: these ones should be moved to the mathjax macros:
   s/\\overbar${r1}/\\overline{$1}/g ;
   s/\\textAndIndex${r1}/$1/g ;
   s/\\implies/\\Rightarrow/g ;

   s/^ *%.*// ;
   s/^ *\\input.*// ;
   s/^ *\\renewcommand.*// ;
   s/^ *\\newcommand.*// ;
   s/^ *\\usepackage.*// ;
   s/^ *\\beginArtNoToc.*// ;
   s/^ *\\EndArticle.*// ;
   s/^ *\\EndNoBibArticle.*// ;
   s/^ *\\generatetitle{(.*)}/$1/ ;
   s/^ *\\label{chap:.*// ;

   s,{ *\\em\s+(.*?)},<em>$1</em>,g;

#\cref{fig:lecture2:lecture2Fig1}
   s/\\cref{(.*?)Fig(\d+)}/fig. $2/g ;

#\imageFigure{../../figures/ece1254/lecture2Fig1}{Sample resistive circuit}{fig:lecture2:lecture2Fig1}{0.3}
   s/^ *\\imageFigure{(.*?)Fig(\d+)}{(.*?)}.*/${1}Fig$2: fig. $2.  $3/ ;

   s/\bcref{/ref{/g ;
   s/\beqnref/ref{/g ;
   s/\bEqnref/ref{/g ;

   s,{dmath},{equation}, ;
   s,\\section{(.*)},<h2>$1</h2>,;
   s,\\subsection{(.*)},<h3>$1</h3>,;
   s,\\paragraph{(.*)},<h3>$1</h3>,;

   s,\\begin{itemize},<ul>, ;
   s,\\end{itemize},</ul>, ;
   s,\\begin{enumerate},<ol>, ;
   s,\\end{enumerate},</ol>, ;
   s,\\item,<li>, ;

   print "$_\n" ;
}

exit 0 ;

sub slurpLatex
{
   my $failedOpen = 0 ;
   my @lines = () ;

   open my $fh, "<$filebase.tex" or $failedOpen = 1 ;
   if ( $failedOpen )
   {
      open $fh, "<$filebase.ltx" or die "could not open '$filebase.tex' nor '$filebase.ltx'\n" ;
   }

   while (<$fh>)
   {
      push( @lines, $_ ) ;
   }
   close $fh ;

   return \@lines ;
}
