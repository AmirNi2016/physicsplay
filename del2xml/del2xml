#!/usr/bin/perl

use strict ;
use warnings ;
use Getopt::Long ;
my $g_deltext ;
my $g_dollar ;
my $g_bPrintHelpAndExit ;

GetOptions
( 
   'delimiter=s'     => \$g_deltext,
   'dollar!'         => \$g_dollar,
   'help!'           => \$g_bPrintHelpAndExit,
) ;

if ( defined $g_bPrintHelpAndExit )
{
   print "del2xml [-help] [-delimiter blah] [-dollar]\n" .
         "\n" .
         "Options:\n" .
         "\t-dollar\tUse '\$' as a delimiter.\n" .
         "\t-delimiter blah\tUse 'blah' as a delimiter.\n" .
         "\n" .
         "Default delimiter is '[opt-spaces],[opt-spaces]\n" .
         "" ;

   exit 1 ;
}

my $g_delimiter ;
if ( defined $g_dollar )
{
   $g_delimiter = qr(\$) ;
}
elsif ( defined $g_deltext )
{
   $g_delimiter = qr($g_deltext) ;
}
else
{
   $g_delimiter = qr(\s*,\s*) ; # opt-spaces comma opt-spaces
}

my @g_headers = () ;
my @g_rows = () ;
my @g_sizes = () ;
my $g_numTags ;
my $g_rowIter = 0 ;

print q(<?xml version="1.0" encoding="utf-8"?>
<DataSet xmlns="http://www.ibm.com/qmf" xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance">

  <ResultSet>
    <MetaData>
      <SourceDescription/>
) ;

while ( <> )
{
   chomp ; # take off the trailing newline character.

   consumeOneLine( $_ ) ;
}

print "      <ColumnsAmount>$g_numTags</ColumnsAmount>\n" ;

for ( my $c = 0 ; $c < $g_numTags ; $c++ )
{
   printOneColumnMetaData( $c ) ;
}

print q(
    </MetaData>
    <Data>
) ;

for ( my $r = 0 ; $r < $g_rowIter ; $r++ )
{
   printOneDataRow( $r, $g_rows[$r] ) ;
}

print q(
     </Data>
  </ResultSet>

  <Extensions/>

</DataSet>
) ;

exit 0 ;

sub consumeOneLine
{
   my ( $line ) = @_ ;

   my @e = split( $g_delimiter, $line ) ;
   my $numColumns = scalar( @e ) ; 

   if ( defined $g_numTags )
   {
      die "numColumns != numTags: $numColumns, $g_numTags\n" if ( $numColumns != $g_numTags ) ;

      $g_rows[$g_rowIter] = \@e ;

      my $i = 0 ;
      foreach (@e)
      {
         my $l = length( $_ ) ;

         if ( $g_sizes[$i] < $l )
         {
            $g_sizes[$i] = $l ;
         }

         $i++ ;
      }

      $g_rowIter++ ;
   }
   else
   {
      @g_headers = @e ;

      for ( my $i = 0 ; $i < $numColumns ; $i++ )
      {
         $g_sizes[$i] = 0 ;
      }

      $g_numTags = $numColumns ;
   }
}

sub printOneColumnMetaData
{
   my ( $c ) = @_ ;       

   my $cpp = $c + 1 ;
   my $n = $g_headers[ $c ] ;
   my $s = $g_sizes[ $c ] ;

   print qq(
      <ColumnDescription id="$cpp">
        <Name>$n</Name>
        <Label>$n</Label>
        <Type>char</Type>
        <Width>$s</Width>
        <Nullable>false</Nullable>
        <Format>plain</Format>
      </ColumnDescription>
) ;
}

sub printOneDataRow
{
   my ( $r, $cr ) = @_ ;
   my @c = @$cr ;

print qq(      <Row id="$r">\n) ;

   for ( my $i = 0 ; $i < $g_numTags ; $i++ )
   {
      my $cpp = $i + 1 ;
      my $sz = $g_sizes[$i] ;
      my $cv = $c[$i] ;
      my $v = sprintf( "%-${sz}s", $cv ) ;

      print qq(        <Cell id="$cpp">$v</Cell>\n) ;
   }

print qq(      </Row>\n) ;
}
